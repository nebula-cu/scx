#!/usr/bin/env bpftrace

/*
 * Note: Requires root privileges to run.
*/

BEGIN
{
    printf("Measuring Local Timer IRQ handling latency... Hit Ctrl-C to end.\n");
}

tracepoint:irq_vectors:local_timer_entry
/cpu == $1/
{
    @start[comm] = nsecs;
}

tracepoint:irq_vectors:local_timer_exit
/cpu == $1/
{
    if(!@start[comm]) {
        return;
    }

    $duration = (nsecs - @start[comm]);

    @max_ns[comm] = max($duration);
    @latency_stats_ns[comm] = stats($duration);
}

interval:s:$2
{
    time("%H:%M:%S\n");
    print("--- Max Latency ---");
    print(@max_ns);
    print("--- Latency Stats ---");
    print(@latency_stats_ns);

    clear(@start);
    clear(@max_ns);
    clear(@latency_stats_ns);
}

END
{
    clear(@start);
    clear(@max_ns);
    clear(@latency_stats_ns);
}

